(ns roll.generate-ami-lookup
  (:require [lumo.io :as io]))

(def fs (js/require "fs"))
(def https (js/require "https"))

(defn -main [& argv]
  (let [json (atom "")]
    (.get https "https://cloud-images.ubuntu.com/locator/ec2/releasesTable"
          (fn [res]
            (.setEncoding res "utf8")
            (.on res
                 "data"
                 (fn [chunk]
                   (swap! json #(str % chunk))))
            (.on res
                 "end"
                 (fn []
                   (let [json' @json]
                     ;; I think the ubuntu json is generated by hand or
                     ;; something, it contains a trailing comma which isn't
                     ;; valid. This removes it
                     (io/spit
                       "src/roll/ami-lookup.edn"
                       (pr-str
                         (into {}
                               (for [[region ubuntu-name version cpu amazon-thing date html-blob]
                                     (.-aaData (js/JSON.parse
                                                 (str (.slice json' 0 -6)
                                                      (.slice json' -5))))
                                     :when (and (= ubuntu-name "xenial")
                                                (= cpu "amd64")
                                                (= amazon-thing "hvm:ebs-ssd"))]
                                 [region (second (re-find #">(ami-\w+)</a>" html-blob))])))))))))))
